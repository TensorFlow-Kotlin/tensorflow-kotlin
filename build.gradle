plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "c"
    id "application"
}

mainClassName = 'io.mattmoore.tensorflow.MainKt'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib:1.3.31")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit")
    // testImplementation('junit:junit:4.12')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

run {
    systemProperty "java.library.path", file("${buildDir}/libs/hello/shared").absolutePath
}

test {
    systemProperty "java.library.path", file("${buildDir}/libs/hello/shared").absolutePath
}

model {
  platforms {
    x64 {
        architecture "x64"
    }
  }

  components {
    hello(NativeLibrarySpec) {
        binaries.all {
        if (targetPlatform.operatingSystem.macOsX) {
            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
            cCompiler.args '-mmacosx-version-min=10.4'
            linker.args '-mmacosx-version-min=10.4'
        } else if (targetPlatform.operatingSystem.linux) {
            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
            cCompiler.args '-D_FILE_OFFSET_BITS=64'
        } else if (targetPlatform.operatingSystem.windows) {
            cCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
            cCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/win32"
            linker.args "Shlwapi.lib", "Advapi32.lib"
        } else if (targetPlatform.operatingSystem.freeBSD) {
            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/freebsd"
        }
      }
    }
  }
}

test.dependsOn  'helloSharedLibrary'
run.dependsOn  'helloSharedLibrary'
